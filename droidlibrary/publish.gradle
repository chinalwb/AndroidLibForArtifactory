apply plugin: 'maven-publish'

def LIB_GROUP_ID = 'com.chinalwb.libraries'
def LIB_ARTIFACT_ID = 'droidlib'
def LIB_VERSION = '2.0.3-SNAPSHOT'

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

publishing {
    repositories {
        maven {
            name = "GithubPackages"
            url = uri("https://maven.pkg.github.com/chinalwb/AndroidLibForArtifactory")
            credentials {
                username = System.getenv('GITHUB_USER') ?: project.properties['GITHUB_USER']
                password = System.getenv('GITHUB_PERSONAL_ACCESS_TOKEN') ?: project.properties['GITHUB_PERSONAL_ACCESS_TOKEN']
            }
        }
        maven {
            name = 'CustomMavenRepo'
            url = "file://${buildDir}/repo"
        }
    }
    publications {
        droidlib(MavenPublication) {
            groupId LIB_GROUP_ID
            artifactId LIB_ARTIFACT_ID
            version LIB_VERSION
            artifact("$buildDir/outputs/aar/droidlibrary-release.aar")
            artifact(sourceJar)
        }
    }
}
